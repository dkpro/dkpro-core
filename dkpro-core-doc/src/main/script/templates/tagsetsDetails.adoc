<% 
// Copyright 2018
// Ubiquitous Knowledge Processing (UKP) Lab
// Technische UniversitÃ¤t Darmstadt
// 
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
// 
// http://www.apache.org/licenses/LICENSE-2.0
// 
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import org.dkpro.core.doc.Util;
import java.util.regex.Pattern;

def escape(text) 
{
    if (text.contains('=')) {
        text = text.replaceAll('=', '\\\\=');
    }
    if (text.contains('|')) {
        text = text.replaceAll('\\|', '{vbar}');
    }
    return text;
}

def tagText(tag)
{
    def text = escape(tag);
    if (text.contains('`')) {
        text = text.replaceAll('`', '{backtick}');
    }
    return text;
}

def commentText(tag,tagset)
{
    def text = tagset.mapping.layout.getCanonicalComment(tag, true);
    if (text) {
        def lines = stripCommentChar(text.split('\n'));

        // If there is a multiline description, check if the first line starts with the tag. If
        // that is the case, treat all lines as the description. If it is not the case, treat
        // only the last line as the description
        if (lines.size() > 1 && lines[0].startsWith(tag)) {
            // Remove tag
            lines[0] = lines[0].substring(tag.length());
            text = lines.join('\n');
        }
        else {
            text = !lines.isEmpty() ? lines[-1] : '';
        }        
            
        if (text == tag) {
            text = '';
        }
        else if (text ==~ "${Pattern.quote(tag)}\\b.*") {
            text = text[tag.length()..-1].trim();
            if (text.startsWith('-') || text.startsWith(':')) {
                text = text[1..-1].trim();
            }
        }
        
        text = escape(text);
    }

    return text ?: '__No description__';
}

def headerCommentText(tagset)
{
    def text = tagset.mapping.layout.getCanonicalHeaderComment(true) ?: '';

    def lines = stripCommentChar(text.split('\n'));
    
    for (int i = 0; i < lines.size(); i++) {
        def line = escape(lines[i]);
        
        // We null the first line here because we assume it contains the long name and we already
        // have that in the heading.
        lines[i] = i == 0 ? '' : line;
    }
    
    text = lines.join('\n');

    return text ?: '__No description__';
}

def stripCommentChar(lines)
{
    result = [];
    for (int i = 0; i < lines.size(); i++) {
        def line = lines[i].trim();
        
        if (line.startsWith('#')) {
            line = line.length() > 1 ? line[1..-1].trim() : '';
        }
        
        result << line;
    }
    return result;
}

def typeText(tag,tagset)
{
    def t = tagset.mapping.getString(tag);
    if (!t.contains('.')) {
        t = tagset.mapping.getString('__META_TYPE_BASE__') + t;
    }
    return Util.typeLink(t);
}

def defaultTypeText(tagset)
{
    def defaultType = tagset.mapping.getString('*');
    if (defaultType && !defaultType.contains('.')) {
        defaultType = tagset.mapping.getString('__META_TYPE_BASE__') + defaultType;
    }
    return defaultType ? Util.typeLink(defaultType) : 'None';
}

def redirectText(tagset)
{
    def id = tagset.mapping.getString('__META_REDIRECT__');
    id = id.tokenize('/')[-1]; // Keep only filename
    id = id[0..-5]; // Remove .map
    def name = id.tokenize('-')[1..-2].join('-');
    return "<<tagset-${id},${name}>>"
}

def links(tagset)
{
    def html = '++++\n<div style="float:right">\n';
    html += "<a href=\"${tagset.url}\">Edit on GitHub</a><br/>\n";
    if (!tagset.mapping.getString('__META_REDIRECT__')) {
        html += "<a href=\"tagsets/${tagset.id}.json\">WebAnno tagset</a>\n";
    }
    html += '</div>\n++++\n';
    return html;
}

%>

<% tagsets.groupBy { it.value.tool }.each{ tool, toolTagsets -> %>

=== ${tool}

<% toolTagsets
    .sort { a,b ->
        (a.value.name as String) <=> (b.value.name as String) ?:
        (a.value.lang as String) <=> (b.value.lang as String) }
    .each { id, tagset -> %>
[[tagset-${ id }]]
<% if (tagset.mapping.getString('__META_REDIRECT__')) { %>
[discrete]
==== ${tagset.longName} (${tagset.lang})

${links(tagset)}

****
[horizontal, role="small"]
Tagset::              ${tagset.name}
Layer::               ${tool}
Language::            ${tagset.lang}
Redirecting{nbsp}to:: ${redirectText(tagset)}
****
<% } else { %>
==== ${tagset.longName} (${tagset.lang})

${links(tagset)}

****
[horizontal, role="small"]
Tagset::              ${tagset.name}
Layer::               ${tool}
Language::            ${tagset.lang}
Type base::           ${tagset.mapping.getString('__META_TYPE_BASE__') ?: 'None'}
Default{nbsp}type::   ${defaultTypeText(tagset)}
****

.Description
${headerCommentText(tagset)}

// SOURCE LINKS
<% 
def sources = tagset.mapping.keys.sort()
    .findAll { it.startsWith('__META_SOURCE_URL__') };
if (sources) {
%>

.See also
<%
    sources.each { source ->
        def link = tagset.mapping.getString(source).trim();
        if (!link.endsWith(']') && !link.contains('?') && !link.endsWith('/')) {
            // Try extracting a title from the link, i.e. using the file name
            def title = link[link.lastIndexOf('/')+1..-1];
            out.println "* link:${link}[${title}]";    
        }
        else {
            // Looks like the source link contains a title already, use it
            out.println "* link:${link}";    
        }
    } // sources 
}
 %>


// LIST OF MODELS THAT USE THIS TAGSET
<% 
def linkedModels = models
    .findAll { model ->
        model.metadata.entry.any { entry -> 
            entry.@key == "${tool}.tagset" &&
            entry.@value == tagset.name &&
            model.@language == tagset.lang
        }
    };
if (!linkedModels.isEmpty()) { %>
.Models using ${tagset.longName} (${tagset.lang})
<% 
linkedModels.each { model ->
    out.println "* ${Util.modelLink(model)}"
} 
%>
<% } else { %>
__This mapping is not used in any models.__
<% } %>

// TABLE OF TAGS IN THE TAGSET
.Tag mappings in ${tagset.longName} (${tagset.lang})
[options="header",cols="1,1,3", role="small"]
|====
|Tag|Type|Description
<% tagset.mapping.keys.sort()
    .findAll { 
        !it.equals('__META_TYPE_BASE__') && 
        !it.startsWith('__META_SOURCE_URL__') &&
        !it.equals('*') }
    .each { tag -> %>
| `${tagText(tag)}`
| ${typeText(tag,tagset)}
| ${commentText(tag,tagset) ?: '__No description__'}
<% } // mapping { %>
|====

<% } // redirect vs proper tagset %>

<% } // toolTagsets %>

<% } // tagsets %>
