<%
import Util;

def escape(text) 
{
    if (text.contains('=')) {
        text = text.replaceAll('=', '\\\\=');
    }
    if (text.contains('|')) {
        text = text.replaceAll('\\|', '{vbar}');
    }
    return text;
}

def tagText(tag)
{
    def text = escape(tag);
    if (text.contains('`')) {
        text = text.replaceAll('`', '{backtick}');
    }
    return text;
}

def commentText(tag,tagset)
{
    def text = tagset.mapping.layout.getCanonicalComment(tag, true);
    if (text) {
        def lines = stripCommentChar(text.split('\n'));

        // If there is a multiline description, check if the first line starts with the tag. If
        // that is the case, treat all lines as the description. If it is not the case, treat
        // only the last line as the description
        if (lines.size() > 1 && lines[0].startsWith(tag)) {
            // Remove tag
            lines[0] = lines[0].substring(tag.length());
            text = lines.join('\n');
        }
        else {
            text = !lines.isEmpty() ? lines[-1] : '';
        }        
            
        if (text == tag) {
            text = '';
        }
        else if (text.startsWith(tag)) {
            text = text[tag.length()..-1].trim();
            if (text.startsWith('-')) {
                text = text[1..-1].trim();
            }
        }
        
        text = escape(text);
    }

    return text ?: '__No description__';
}

def headerCommentText(tagset)
{
    def text = tagset.mapping.layout.getCanonicalHeaderComment(true) ?: '';

    def lines = stripCommentChar(text.split('\n'));
    
    for (int i = 0; i < lines.size(); i++) {
        def line = escape(lines[i]);
        
        // We null the first line here because we assume it contains the long name and we already
        // have that in the heading.
        lines[i] = i == 0 ? '' : line;
    }
    
    text = lines.join('\n');

    return text ?: '__No description__';
}

def stripCommentChar(lines)
{
    result = [];
    for (int i = 0; i < lines.size(); i++) {
        def line = lines[i].trim();
        
        if (line.startsWith('#')) {
            line = line.length() > 1 ? line[1..-1].trim() : '';
        }
        
        result << line;
    }
    return result;
}

def typeText(tag,tagset)
{
    def t = tagset.mapping.getString(tag);
    if (!t.contains('.')) {
        t = tagset.mapping.getString('__META_TYPE_BASE__') + t;
    }
    return Util.typeLink(t);
}

def defaultTypeText(tagset)
{
    def defaultType = tagset.mapping.getString('*');
    if (defaultType && !defaultType.contains('.')) {
        defaultType = tagset.mapping.getString('__META_TYPE_BASE__') + defaultType;
    }
    return defaultType ? Util.typeLink(defaultType) : 'None';
}

def redirectText(tagset)
{
    def id = tagset.mapping.getString('__META_REDIRECT__');
    id = id.tokenize('/')[-1]; // Keep only filename
    id = id[0..-5]; // Remove .map
    def name = id.tokenize('-')[1..-2].join('-');
    return "<<tagset-${id},${name}>>"
}
%>

<% tagsets.groupBy { it.value.tool }.each{ tool, toolTagsets -> %>

=== ${tool}

<% toolTagsets
    .sort { a,b ->
        (a.value.name as String) <=> (b.value.name as String) ?:
        (a.value.lang as String) <=> (b.value.lang as String) }
    .each { id, tagset -> %>
[[tagset-${ id }]]
<% if (tagset.mapping.getString('__META_REDIRECT__')) { %>
[discrete]
==== ${tagset.longName} (${tagset.lang})

${Util.editOnGithub(tagset.url)}

[small]#*_Tagset:_* __${tagset.name}__# +
[small]#*_Layer:_* __${tool}__# +
[small]#*_Language:_* __${tagset.lang}__# + 
[small]#*_Redirecting to:_* __${redirectText(tagset)}__# + 
<% } else { %>
==== ${tagset.longName} (${tagset.lang})

${Util.editOnGithub(tagset.url)}

[small]#*_Tagset:_* __${tagset.name}__# +
[small]#*_Layer:_* __${tool}__# +
[small]#*_Language:_* __${tagset.lang}__# + 
[small]#*_Type base:_* __${tagset.mapping.getString('__META_TYPE_BASE__') ?: 'None'}__# +
[small]#*_Default type:_* __${defaultTypeText(tagset)}__#

// DESCRIPTION
${headerCommentText(tagset)}

// SOURCE LINKS
<% 
def sources = tagset.mapping.keys.sort()
    .findAll { it.startsWith('__META_SOURCE_URL__') };
if (sources) {
    out.println ".See also"
    sources.each { source ->
        def link = tagset.mapping.getString(source).trim();
        if (!link.endsWith(']') && !link.contains('?') && !link.endsWith('/')) {
            // Try extracting a title from the link, i.e. using the file name
            def title = link[link.lastIndexOf('/')+1..-1];
            out.println "* link:${link}[${title}]";    
        }
        else {
            // Looks like the source link contains a title already, use it
            out.println "* link:${link}";    
        }
    } // sources 
}
 %>


// LIST OF MODELS THAT USE THIS TAGSET
<% 
def linkedModels = models
    .findAll { model ->
        model.metadata.entry.any { entry -> 
            entry.@key == "${tool}.tagset" &&
            entry.@value == tagset.name &&
            model.@language == tagset.lang
        }
    };
if (!linkedModels.isEmpty()) { %>
[discrete]
===== Models using ${tagset.longName} (${tagset.lang})
<% 
linkedModels.each { model ->
    out.println "* ${Util.modelLink(model)}"
} 
%>
<% } else { %>
__This mapping is not used in any models.__
<% } %>

// TABLE OF TAGS IN THE TAGSET
[discrete]
===== Tag mappings in ${tagset.longName} (${tagset.lang})
[options="header",cols="1,1,3"]
|====
|Tag|Type|Description
<% tagset.mapping.keys.sort()
    .findAll { 
        !it.equals('__META_TYPE_BASE__') && 
        !it.startsWith('__META_SOURCE_URL__') &&
        !it.equals('*') }
    .each { tag -> %>
| `${tagText(tag)}`
| ${typeText(tag,tagset)}
| ${commentText(tag,tagset) ?: '__No description__'}
<% } // mapping { %>
|====

<% } // redirect vs proper tagset %>

<% } // toolTagsets %>

<% } // tagsets %>
